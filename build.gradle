import it.bitrule.plorex.script.ASpotlessPlugin
import it.bitrule.plorex.script.CommonPlugin
import it.bitrule.plorex.script.PublishPlugin

plugins {
    id 'java'
    id 'maven-publish'
    id 'idea'
    alias libs.plugins.shadow
    alias libs.plugins.bukkitPluginYAML
}

apply plugin: "java"
apply plugin: ASpotlessPlugin
apply plugin: PublishPlugin
apply plugin: CommonPlugin

repositories {
    mavenLocal()
}

dependencies {
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'

    compileOnly this.libs.gson.get()

    compileOnly files("lib/PaperSpigot-1.8.8-R0.1-SNAPSHOT.jar")

    compileOnly(libs.annotations.get())
    compileOnly(libs.mongodb.get())
    implementation(libs.miwiklark.get())

    compileOnly(libs.lombok.get())
    annotationProcessor(libs.lombok.get())

    testCompileOnly(libs.lombok.get())
    testAnnotationProcessor(libs.lombok.get())
}

tasks {
    build {
        dependsOn spotlessApply
        dependsOn shadowJar
    }

    shadowJar {
        archiveFileName.set("${rootProject.name}-v${version}.jar")

        relocate 'com.mongodb', 'it.bitrule.relocate.mongodb'
        relocate 'org.bson', 'it.bitrule.relocate.bson'
    }

    compileJava {
        options.encoding = "UTF-8"
        options.compilerArgs.add("-Xpkginfo:always")
    }
}

//Automatically download dependencies source code
idea {
    module {
        downloadSources = true
        downloadJavadoc = false
    }
}

tasks.withType(AbstractCopyTask).configureEach {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

java {
    withSourcesJar()
}

bukkit {
    main = "it.bitrule.tebex.TebexPlugin"
    version = "${project.version}"
    authors = ["bitrule"]
    description = "${findProperty("plugin-description")}"
    name = "${findProperty("plugin-name")}"

    commands {
        "redeem" {
            description = "Redeem a code"
            permission = "tebex.redeem"
            usage = "/redeem <code>"
        }
    }
}